# TIP 560: Megawidget Configure/Property Support
        Author:         Donal K. Fellows <donal.k.fellows@manchester.ac.uk>
        State:          Draft
        Type:           Project
        Vote:           Pending
        Created:        23-Jan-2020
        Post-History:
        Tcl-Version:    8.7
        Keywords:       Tk, TclOO, properties
		Tk-Branch:      tip-560
------

# Abstract

This TIP is a companion for [TIP #558](558.md); it describes how to build a
property system based on TclOO that can support making Tk megawidgets.

# Rationale and Design Requirements

Tk megawidgets should be a natural fit for TclOO, as Tk widgets have long
behaved like classes. However, configuration of Tk-like objects is quite
complex and has long been one of the most awkward parts for authors of
megawidgets, often leading to only partial implementations. It is also rather
different to the simple system described in [TIP #558](558.md). In particular,
properties can be configured from defaults, from the option database, or
explicitly, and two methods, **configure** and **cget**, to handle scripted
access; **configure** returns a property descriptor when reading, whereas
**cget** just reads the value of the property.

It should be noted that this scheme is _semantically incompatible_ with the
**configure** of [TIP #558](558.md); the results of **configure** are entirely
different, with one returning the property value itself and the other
returning a property descriptor (a short list); both mechanisms are present
"in the wild" so attempting to unify this is extremely difficult with how
things stand. Therefore it is a _non-goal_ of this TIP to design a system that
can allow an object to be accessed by both systems at once.

There are also properties that can only be configured during widget creation
(e.g., the **-use** option of toplevels and the **-class** option of quite a
few widgets) though all properties are always readable. Another complexity is
that calls to **configure** are transactional; no changes are applied unless
all changes are applied (though a useless redisplay might be
triggered). What's more, any **-class** option needs to be handled early as it
changes how the configuration database is read from. (Indeed, this is omitted
from the standard mechanism, just as it is also handled specially in Tk frame
widgets and so on.)

Finally, there should be a mechanism for supporting aliases of properties.

In support of this, we will want to support typing of properties as this is a
common feature of Tk widgets. While there is a substantial set of standard
types (such as strings, colors, images, and screen distances) it is an open
set: we need a way of allowing user code to add custom types. A common custom
type is the table-driven type, where values must be chosen from a given list
of strings but can be abbreviated, so we should ensure that we provide special
support for that.

# Specification

Tk will supply a TclOO class, **tk::configurable**, that classes may inherit
from to gain a **configure** and a **cget** method, as well as a non-exported
**Initialise** method (that may only be called once; subsequent calls will do
nothing) intended to be used from constructors, and a non-exported
**PostConfigure** method intended as a point for user- and
subclass-interception. In addition, Tk will supply a metaclass,
**tk::megawidget**, that will allow the creation of definitions suitable for
configuration (that class may gain other behaviours in the future). As with
the **oo::configurable** metaclass, property names will be given without
leading hyphens when they are specified.

    tk::megawidget create myLabel {
        superclass tk::configurable

        property label
        property borderwidth -type distance -default 1px \
            -name borderWidth -class BorderWidth
        property bd -alias borderwidth
    }

As we can see from this, we want to support some options for a property. The
full list (not fully shown above) is:

* **-name** *optName*

    This gives the name used for looking up a default in the options
    database. It defaults to the main name of the property with **string
    tolower** applied to it.

* **-class** *clsName*

    This gives the class name used for looking up a default in the options
    database (this is not a TclOO name). It defaults to the main name of the
    property with **string totitle** applied to it.

* **-default** *value*

    The default value of the option, used when no other default is
    available. Its default will depend on the type of the option (see below),
    but is usually either the empty string or a zero value. The default will
    be validated against the type.

* **-type** *typeName*

    The type of the value. The types will be subcommands of an ensemble (see
    **Property Types** below) that will provide defaults and validation. The
    default type will be **string**, which will do no validation and use a default
    that is empty.

* **-initonly** *boolean*

    Controls whether this is an initialisation only property. Initialisation
    only properties may only be set by the **Initialise** method, and not by
    **configure**. (For example, real initialise-only properties are the
    **-use** property of toplevels and the **-container** property of frames.)
    Properties are not initialisation only by default; this is expected to be
    a rare use case.

* **-alias** *propertyName*

    Makes this property an alias for another property, which must exist at
    this point. The other options to **property** (as described above) will be
    illegal if this is given. Note that alias properties are not set by
    initialisation (unless explicitly provided), since their underlying
    property should be set instead.

The storage mechanism for properties will be fixed to be the array in the
object instance with the empty local name (so the property `foo` will be in
array element variable `(foo)` in the instance namespace; this is a trick
stolen from stooop). State saving and reloading (so that changes are
approximately transactional) will be done with **array get** and **array
set**, and will *not* go via the validation.

The **configure** and **cget** methods will work in ways that should be
immediately recognisable to Tk users. There will also be a non-exported
**PostConfigure** method (taking no arguments) that will be called by
**configure** after any call that could have changed the state (no
determination will be done of whether the state actually changed); the default
implementation of **PostConfigure** will be empty, but it will provide a
convenient place to hook generation of events for state changes or validation
of the whole configuration (errors will trigger the same rollback behavior as
validation failures). It will be recommended (but out of the scope of this TIP
to implement) that idle events are used to combine state update events.

Initialisation will be done by calling the non-exported **Initialise** method,
which will take its first argument to be the widget path name (we do not
assume that this is the same as the object name) and an even number of
following arguments that will be the same as if for **configure**. The
initialisation will write _all_ elements of the array, using the information
from the property configuration and retrieved from the option database (see
**option get**), and is the only method that will write initialisation-only
elements. Note that this method is intended to be called from a constructor,
and will _not_ call the **PostConfigure** method or perform state rollback on
failure; the caller can do any system validation afterwards, and validation
failures are expected to abort widget creation altogether rather than rolling
anything back.

## Interaction with Fundamental TclOO and Tk Mechanisms

As all properties are readable in Tk, all will be listed in the readable
properties of the class. Most properties will also be listed in the writable
properties of the class, but initialisation only properties will not.

The following methods will be created for each property (where _name_ is the
hyphen-prefixed version of the name):

* **<OptDescribe**_name_**>** — takes no arguments and returns a description
  of the parts of the property descriptor that do not change. In particular,
  this is a list of three items; these are the option database name of the
  property, the option database class of the property, and the actual default
  value of the property.

* **<OptValidate**_name_**>** — takes a single value, checks whether the value
  is of the type of the property, and returns the normalized property value.
  Typically forwarded to the **validate** method of the property's type
  object.

Again, also note that the actual storage of the options is the array in the
class with the empty name. C code may need to set traces on elements of the
array; these may be established before or after the **Initialise** method is
called.

Note that **Initialise** requires an existing widget name. A consequence of
that is that any true initialisation-only properties that need to be passed to
that widget must be manually parsed before the widget is created (_or_ the
widget can be created, used for parsing, and then destroyed and rebuilt with
the correct options; that's not too expensive if the temporary widget is never
mapped).

## Property Types

One key part of this specification is a system for typing of properties, since
it is extremely common for Tk widget properties to be constrained to be of
particular types. This will be done using an ensemble of type implementation
commands, **tk::PropType**, with the member elements of the ensemble being
themselves ensemble-like (probably objects, but not necessarily), supporting
at least two subcommands, **validate** and **default**.

The **validate** subcommand will take a single argument, the value to be
validated, and will produce an error if the validation fails and return the
value to be actually set otherwise (to allow a value to be converted to
canonical form if desired). The **default** subcommand will take no arguments,
and return the default value usually associated with the type. (Note that
there is no need to make either of these commands aware of which class or
instance they’re being used with; types are independent of how they are used
and these defaults can be overridden when the property is created.)

For example, this will allow the validation of a proposed value, *$foobar*,
for a property of type *$gorp*, to be done by calling:

    tk::PropType $gorp validate $foobar

The standard types will be:

* **boolean**: value acceptable to **string is boolean -strict** (e.g.,
  **true** or **off**). Defaults to **false**.

* **zboolean**: empty string or boolean. Defaults to the empty string.

* **string**: any string. Defaults to the empty string.

* **integer**: any integer (i.e., acceptable to **string is entier
  -strict**). Defaults to **0**.

* **zinteger**: any integer (as above) or empty string. Defaults to the empty string.

* **float**: any float (except NaN) acceptable to **string is double
  -strict**. Defaults to **0.0**.

* **zfloat**: any float (as above) or empty string. Defaults to the empty string.

* **distance**: any screen distance (as parsed by **winfo fpixels**). Defaults
  to **0p**.

* **image**: name of any Tk image, or empty string. Defaults to the empty string.

* **color**: any color (as parsed by **winfo rgb**). Defaults to **black**.

* **zcolor**: any color, or empty string. Defaults to the empty string.

* **font**: any font (as parsed by **font actual**). Defaults to **TkDefaultFont**.

* **relief**: any relief (**flat**, **groove**, **raised**, **ridge**,
  **solid**, or **sunken**) or unambiguous prefix. Defaults to **flat**.

* **justify**: any justification (**left**, **right**, or **center**) or
  unambiguous prefix. Defaults to **left**.

* **anchor**: any anchor (**n**, **ne**, **e**, **se**, **s**, **sw**, **w**,
  **nw**, or **center**) or unambiguous prefix. Defaults to **center**.

* **list**: any valid Tcl list. Defaults to the empty list (and empty string).

* **dict**: any valid Tcl dictionary. Defaults to the empty dict (and empty string).

The official mechanism for adding a new type will be via the class
**tk::propertytype**. Instances of that will automatically plug themselves
inside using their names, and will be implemented using a callback provided to
the constructor. This will result in a class definition (approximately) like
this:

    oo::class create tk::propertytype {
       constructor {default testCommand} {
          ... # trivial implementation that saves the params
       }
    
       method validate {value} {
          if {![{*}$testCommand $value]} {
             return -code error ... # error message generation
          }
          return $value
       }
    
       method default {} {
          return $default
       }
    
       self {
          # class-level definitions
       }
    }

In practice, things are more complex because there are three basic ways to
validate a value. In particular, there are types for which there are tests
that return a boolean, types for which there are parsers that error on
failure, and types that are driven by a table of permitted values. As such,
**tk::propertytype** is actually an abstract class and there are concrete
implementations of each of the validation options.

When a type is created with **tk::propertytype createbool**, a boolean test is
expected to be provided as a command fragment that takes a single extra
argument. An example of the use is this, which makes the **boolean** type
described above:

    tk::propertytype createbool boolean "false" {
        string is boolean -strict
    }

When a type is created with **tk::propertytype createthrow**, the test instead
is expected to throw an error on failure. Because this can be more complex, we
assume that the value being tested is passed in the (local) variable
`$value`. An example of the use is this, which makes the **distance** type
described above:

    tk::propertytype createthrow distance "0px" {
        winfo fpixels . $value
    }

When a type is created with **tk::propertytype createtable**, the test is
driven by **tcl::prefix match** and all the caller has to do is supply the
table. An example of this one is:

    tk::propertytype createtable justify "left" {
        center left right
    }

It is up to the caller to ensure that each type's default values actually pass
validation checks.

Note that all of the types above are created with unqualified names; the names
are mangled internally by the above methods so that they plug in correctly
into the **tk::PropType** ensemble.

# Implementation

See the [`tip-560`](https://core.tcl-lang.org/tk/timeline?r=tip-560) branch.

# Copyright

This document is placed in the public domain.
