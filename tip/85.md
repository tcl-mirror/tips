# TIP 85: Custom Comparisons in Tcltest
	Author:         Arjen Markus <arjen.markus@wldelft.nl>
	Author:         Don Porter <dgp@users.sf.net>
	State:          Final
	Type:           Project
	Vote:           Done
	Created:        31-Jan-2002
	Post-History:   
	Keywords:       test,string comparison,floating-point
	Tcl-Version:    8.4
-----

# Abstract

This TIP proposes a simple mechanism to make the _tcltest_ package
an even more flexible package than it already is by allowing the
programmer to define his or her own comparison procedures.  Such
procedures can deal with issues like allowing a \(small\) tolerance in
floating-point results.

# Rationale

The _test_ command of the package _tcltest 2.0_ supports the
comparison of the actual result with the expected result by a number
of methods: exact matching, glob-style matching and matching via a
regular expression, according to the _-match_ option.  The flexibility
is indeed enhanced over the package _tcltest 1.0,_ as it is now much
easier to allow for small variations in _string_ results.  But
it is nearly impossible to define an accurate test that checks if
floating-point results are the "same" - exact matching will seldom
suffice due to platform-specific round-off errors or differences in
formatting a floating-point number \(_0.12_ versus _.12_ for
instance\).

It is also impossible to compare results that are not easily expressed
as strings, for instance an application that produces binary files
that need to be compared or simply very long strings - these could
easily be stored in an external file, but would be awkward in a file
with a large number of such tests.

# Proposal

The package _tcltest 2.0.2_ defines an internal comparison procedure,
_CompareStrings_ that performs matching according to the three built-in
_-match_ options of _test_.  This
procedure can easily be replaced by one that invokes registered 
commands or procedures. Such a command or procedure takes two 
arguments and returns 1 for a match and a 0 for failure, 
just as _CompareStrings_ does in the current implementation:

	 proc myMatchProc { expected actual } { 
	   if { $expected (is somehow equal) $actual } {
	      return 1
	   } else
	      return 0
	   }
	 }

A new public command _customMatch_ is proposed for the purpose
of registering these matching commands.  It can register a procedure,
such as _myMatchProc_ defined above:

	 ::tcltest::customMatch mytype myMatchProc

or, as in the sample implementation, an incomplete command:

	 ::tcltest::customMatch exact [list ::string equal]

When the _test_ command is called with the _-match mytype_ option,
the command _myMatchProc_ will be completed with two arguments,
the expected and actual results, and will be evaluated in the global
namespace to determine whether the test result matches the expected
result.  Likewise, the _test_ option _-match exact_ will
cause matching to be tested by the command _::string equal_.
The default value of the _-match_ option will continue to be _exact_.

Allowing procedures to be invoked by their type names gives us the 
flexibility to register as many such procedures or commands as required.

Because this proposal adds a new public command to the _tcltest_
package, the version will be incremented to 2.1.

A patch to the current HEAD that implements this proposal is
available as Tcl Patch 521362 at the Tcl project at SourceForge.
<http://sf.net/tracker/?func=detail&aid=521362&group\_id=10894&atid=310894>

# Two Examples

To show how this works, we include two simple examples:

 * Testing a package for calculating mathematical functions like
   Bessel functions.

 * Testing for negative results, as when providing an alternative, but
   incompatible implementation of a feature.

First, suppose you have defined a package for calculating the value of
a general Bessel function, just the sort of function that returns
floating-point numbers.  Then the results may be imprecise due to
rounding-off errors, different values of _tcl\_precision_ or, even
more banally, differences in the formatting of floating-point numbers
\(_0.12_ versus _.12_ for instance\). 

The following shows how to do this:

	 #
	 # Test implementation of Bessel functions
	 # (Table only provides 4 decimals)
	 #
	 customMatch 4decimals matchFloat4Decimals
	
	 proc matchFloat4Decimals { expected actual } {
	    return [expr {abs($expected-$actual) <= 0.5e-4}]
	 }
	
	 test "J0-1.1" "J0 for x=1.0" -match 4decimals -body {
	    J0 1.0
	 } -result 0.7652
	
	 test "J1-1.1" "J0 for x=1.0" -match 4decimals -body {
	    J1 1.0
	 } -result 0.4401

The second example occurs for instance when testing alternative
implementations: you want to check that the original standard feature
is failing whereas the new but incompatible alternative gets it right.
Then:

	 proc matchNegative { expected actual } {
	    set match 0
	    foreach a $actual e $expected {
	       if { $a != $e } {
	          set match 1
	          break
	       }
	    }
	    return $match
	 }
	
	 customMatch negative matchNegative
	
	 #
	 # Floating-point comparisons are imprecise. The following
	 # test returns typically such a list as {643 1357 1921 79 781 1219}
	 # so nothing even close to the expected values.
	 # 
	 test "ManyCompares-1.2" "Compare fails - naive comparison" \
	    -match negative -body {
	    set naiv_eq 0
	    set naiv_ne 0
	    set naiv_ge 0
	    set naiv_gt 0
	    set naiv_le 0
	    set naiv_lt 0
	
	    for { set i -1000 } { $i <= 1000 } { incr i } {
	       if { $i == 0 } continue
	
	       set x [expr {1.01/double($i)}]
	       set y [expr {(2.1*$x)*(double($i)/2.1)}]
	
	       if { $y == 1.01 } { incr naiv_eq }
	       if { $y != 1.01 } { incr naiv_ne }
	       if { $y >= 1.01 } { incr naiv_ge }
	       if { $y >  1.01 } { incr naiv_gt }
	       if { $y <= 1.01 } { incr naiv_le }
	       if { $y <  1.01 } { incr naiv_lt }
	    }
	    set result [list $naiv_eq $naiv_ne $naiv_ge $naiv_gt $naiv_le $naiv_lt]
	 } -result {2000 0 2000 0 2000 0}

makes sure that a mismatch is treated as the expected outcome.

# Alternatives and objections

Of course, it is possible to achieve these effects within the current
framework of _tcltest_, by putting these match procedures inside the
body of the test case. No extra user command would be necessary then.

There are at least two drawbacks to this approach:

 * The result against which we want to match is hidden in the code

 * If the test fails, the actual result is not printed \(at least not
   by the _tcltest_ framework\).

As a matter of fact, the proposed mechanism actually simplifies the 
current implementation of the three match types to a certain degree by 
turning a switch between the three types into an array index.

# See Also

Tcl Feature Request 490298.
<http://sf.net/tracker/?func=detail&aid=490298&group\_id=10894&atid=360894>

# History

_Cameron Laird_ was quite enthousiastic about the idea of providing 
custom match procedures.

_Mo DeJong_ requested the explicit examples \(the second is actually 
the situation that triggered this TIP in the first place\).

_Don Porter <dgp@users.sf.net>_ revised the registration mechanism 
such that an arbitrary set of matching commands or procedures can be supported. His suggestions led to a revision of the TIP. He also 
revised the draft implementation.

# Copyright

This document is placed in the public domain.

