# TIP 490: msgcat for tcloo
	Author:         Harald Oehlmann <oehhar@sourceforge.com>
	State:          Draft
	Type:           Project
	Vote:           Pending
	Created:        07-Dec-2017
	Post-History:
	Keywords:       msgcat, oo
	Tcl-Version:    8.7
-----

# Abstract

Package **msgcat** implements message catalogues for packages organized in nested namespaces.
This TIP proposes the extension to TclOO.

# Rationale

Since TclOO was included in the core, packages may also be defined as TclOO classes or classless objects.
The **msgcat** package should feature this.

A package should have its methods within a package namespace:

<pre>
namespace eval ::foo {
    oo::class create Foo
}
package provide foo 1.0
</pre>

The message catlog belongs to the package, not to an individual class.

<pre>
namespace eval ::foo {
    msgcat::mcload $dir/msgs
    oo::class create Foo {
        ...
    }
}
package provide foo 1.0
</pre>

# Documentation

There are 3 use-cases to consider (which may be intermixed in the same package):

(with 'Servus!' as translation for 'Hi!')

## Use-Case 1: the package does not use OO

<pre>
namespace import msgcat::*
namespace eval ::N1 {
    mcload $dir/msgs
    proc m1 {} {
        puts [mc Hi!]
    }
}

% N1::m1
Servus!
</pre>

## Use-case 2: The package implementation is done by a class

<pre>
namespace import msgcat::*
namespace eval ::N2Class {
    mcload $dir/msgs
    oo::class create C1
    oo::define C1 method m1 {
        puts [mc Hi!]
    }
}

# The class object may be used in another namespace
namespace eval ::N2Obj {
    set O1 [::N2Class::C1 new]
}

% $N2Obj::O1 m1
Servus!
</pre>

## Use-case 3: The package implementation is done by a classless object

<pre>
namespace import msgcat::*
namespace eval ::N3 {
    mcload $dir/msgs
    oo::object create O2
    oo::objdefine O2 method m1 {} {
        puts [mc Hi!]
    }
}

% N3::O2 m1
Servus!
</pre>

## Use-case 1 may be emulated in Use-case 2 and Use-case 3 using "namespace eval"

Before this extension, a programmer for use-case 2 and use-case 3 must have used "namespace eval" to explicitly specify the package namespace:

<pre>
namespace import msgcat::*
namespace eval ::N3 {
    mcload $dir/msgs
    oo::object create O2
    oo::objdefine O2 method m1 {} {
        puts [namespace eval ::N3 {mc Hi!}]
    }
}

% N3::O2 m1
Servus!
</pre>

This should still work with the new extension for compatibility reasons.

# Usage

There are 4 extensions proposed:

## Extension 1: Extend all msgcat commands to support all 3 use-cases.

So any **msgcat** command will detect the scenario on its own and extract the package namespace automatically.

The commands which are packet-namespace related are: **mc**, **mcexists**, **mcpackagelocale**, **mcforgetpackage**, **mcpackagenamespaceget** (new command, see below), **mcpackageconfig**, **mcset** and **mcmset**.

This has the following advantages (compared to the alternatives):

* no new commands, no learning
* if another foreign procedure is called and the procedure wants to use the callers message catalog, it may just use "uplevel 1 {msgcat tag}" and does not need to know if it is a class or not.

Here is an example for the second advantage:

The tklib package "tooltip" may invoke `msgcat::mc msg` for all textes to get eventual translations (it does something like that but I don't understand it, IMHO broken).
The package namespace of the caller should be used (not the one of the tooltip package).

So:
<pre>
proc ::tooltip::tooltip {widget message} {
    ...
    set message [uplevel 1 {::msgcat::mc $message}]
}
</pre>

This will work in all use-cases, e.g. if `tooltip::tooltip` is called by a method following use-case 1 to 3.

## Extension 2: new command to get package namespace

The "magic" to extract the package namespace is exposed by the command:

<pre>
mcpackagenamespaceget
</pre>

This may be used:

* for the same case like the upper tooltip example, but for late binding
* for introspection and debugging

The upper tooltip example, where the translation is extracted when the tooltip is actually shown (to show an updated message if the current locale changed)

<pre>
proc ::tooltip::tooltip {widget message} {
    ...
    set messagenamespace [uplevel 1 {::msgcat::mcpackagenamespaceget}]
}

proc ::tooltip::show {widget messagenamespace message} {
    ...
    set message [namespace eval $messagenamespace [list ::msgcat::mc $message]]
}
</pre>

Examples with the 3 use-cases:

### Use-case 1: the package does not use OO

<pre>
namespace eval ::N1 {
    proc m1 {} {msgcat::mcpackagenamespaceget}
}

% N1::m1
::N1
</pre>

### Use-case 2: The package implementation is done by a class

<pre>
namespace eval ::N2Class {
    oo::class create C1
    oo::define C1 method m1 {msgcat::mcpackagenamespaceget}
}

# The class object may be used in another namespace

namespace eval ::N2Obj {
    set O1 [::N2Class::C1 new]
}

% $N2Obj::O1 m1
::N2Class
</pre>

### Use-case 3: The package implementation is done by a classless object

<pre>
namespace eval ::N3 {
    oo::object create O2
    oo::objdefine O2 method m1 {} {msgcat::mcpackagenamespaceget}
}

% N3::O2 m1
::N3
</pre>

### Manually overwrite the package namespace by namespace eval

As stated in the introduction, current oo code might have used an explicit `namespace eval packagenamespace` to make msgcat work. This should still be supported:

<pre>
namespace eval ::N3 {
    oo::object create O2
    oo::objdefine O2 method m1 {} {
        namespace eval ::N4 { msgcat::mcpackagenamespaceget}
    }
}

% N3::O2 m1
::N4
</pre>

Remark, that in most real cases, one will explicitly specify "::N3" instead "::N4", as this is the package namespace.
"::N4" was chosen in the example to show the fact, that the "namespace eval" overwrites any class-magic.

## Extension 3: new command to get a translation with a package namespace as argument

A new command is proposed to get a translation with an explicit namespace:
<pre>
mcn ns src args...
</pre>
with the arguments:
* ns: package namespace to do the translation for
* src: the translation source string (like mc command)
* args: eventual arguments to contained format patterns (like mc command)

This command is identical to the `mc` command, with the difference, that the package namespace is not found by an implicit call to `mcpackagenamespaceget`, but may be explicitly specified as first argument

Then, the mc command may be expressed like:
<pre>
mcn [mcpackagenamespaceget] src args...
</pre>

There are the following purposes for this command:
* foreign packages. The package namespace is known (for example by a call to `mcpackagenamespaceget`). The translation may be retrieved by a call to `mcn` without any `namespace eval $ns` around it.
* Authors of C packages required to specify the namespace explicitly.
* Optimizations in an eventual time critical path. The speed of the old msgcat is beaten by `mcn [namespace current]..`

An example for the case of a foreign package is the tooltip package described above.

The contained call:

<pre>
proc ::tooltip::show {widget messagenamespace message} {
    ...
    set message [namespace eval $messagenamespace [list ::msgcat::mc $message]]
}
</pre>

may be expressed like:

<pre>
proc ::tooltip::show {widget messagenamespace message} {
    ...
    set message [::msgcat::mcn $messagenamespace $message]
}
</pre>


## Extension 4: Command "mcexists" should get a parameter -namespace to explicitly specify the namespace

The command `mcexists` has currently the syntax:

<pre>
mcexists ?-exactnamespace? ?-exactlocale? src
</pre>

A switch ?-namespace ns? is added to specify the namespace explicitly:

<pre>
mcexists ?-exactnamespace? ?-exactlocale? ?-namespace ns? src
</pre>

This may be useful in similar situations as the `mcn` command.

# Implementation

Within **msgcat**, the package namespace is currently extracted by:

<pre>
proc msgcat::mc {src args} {
    ...
    set ns [uplevel 1 {namespace current}]
</pre>

This is replaced by:

<pre>
proc msgcat::mc {src args} {
    ...
    set ns [PackageNamespaceGet]
    ...
}
proc ::msgcat::PackageNamespaceGet {} {
    uplevel 2 {
        # Check for no object
        # (undocumented test proposed by dkf 2018-01-12)
        if { [namespace which self] ne "::oo::Helpers::self"} {
            return [namespace current]
        }
        # Get Class
        set Class [info object class [self]]
        # Check for a classless object
        if {$Class eq {::oo::object}} {
            return [namespace qualifiers [self]]
        }
        # Object defined by class
        return [namespace qualifiers $Class]
    }
}
</pre>

The implementation is in tcl fossil in branch
[tip490-msgcat-oo-2](https://core.tcl.tk/tcl/timeline?r=tip490-msgcat-oo-2).

There are tests but no man page changes jet.
Please use this text as man-page.

# Discussion

## Issue 1: are really all oo use-cases covered ?

Gosts like multiple inheritance, mixins, filters whatever are calling...

## Issue 2: All backward compatibility issues covered ?

I see the following covered ones:

* no oo -> `uplevel 1 namespace current` is used, o.k.
* oo with explicit `namespace eval` -> o.k.

Are there eventually other cases not discussed here?

## Issue 3: performance loss

There is a performance penalty due to the modification described in "implementation".

To not decrease performance would require to introduce new commands, what I think is not good.
We would loose the flexibility and backward compatibility to just insert a 3rd party package, which may call "uplevel 1 msgcat::mc tag", and it works.

For performance enthusiasts, one may use "mcn", which may be little faste than former msgcat.

# Alternatives

## Use a mixin

Proposed by Donal Fellows

Each class which wants to use message catalogs require a call to `mixin msgcat::MessageCatalogAware`.

Then, the following commands are available and correspond to the `msgcat::` commands:

* my mc
* my mcmax
* my mcexists

All other mc-commands are not available within classes and should be used outside of a class.

# Examples

Within the following example, the message catalog for the package is loaded outside the method definitions.

A class "Foo" is created and the msgcat capabilities are activated by the mixin.

Then, a method is defined, which uses the message catalog of the namespace.

<pre>
namespace eval ::foo {
    msgcat::mcload $dir/msgs
    oo::class create Foo {
        mixin msgcat::MessageCatalogAware
        method printMessage {x y} {
            puts [my mc "FooPrintMessage(%d,%d)" $x $y]
        }
    }
}
package provide foo 1.0
</pre>

The implementation is in tcl fossil in branch
[tip490-msgcat-oo](https://core.tcl.tk/tcl/timeline?r=tip490-msgcat-oo).

## Use own commands for getting and setting message catalogues

Eric Boudallier proposed a solution on [Wiki page "msgcat and TclOO"](http://wiki.tcl.tk/39430)

There is also an addendum by Donal Fellows with an alternate implementation.

## mixin solution where superclass and subclass may not be in different namespaces:

Donal Fellows also provided this solution.

The key then is how to make `msgcat::mc` work (and a few related commands as well). The trick I've thought of is to use the magic powers of `apply` and `tailcall`; here's what we're looking for:

<pre>
namespace eval ::foo {
    msgcat::mcload $dir/msgs
    oo::class create MessageCatalogAware {
        forward mc apply {args {tailcall ::msgcat::mc {*}$args} ::foo}
        unexport mc
    }
    oo::class create Foo {
        mixin MessageCatalogAware
        # ...
    }
    oo::class create Bar {
        mixin MessageCatalogAware
        # ...
    }
}
package provide foo 1.0
</pre>

(I'd use a mixin for this; it's not really about the class itself; it's just about making a capability available.)

It'd be nice to have a way to make that bridging class automatically. Here it is.

<pre>
oo::class create ::msgcat::oobridge {
    superclass oo::class
    self method create {name args} {
        set cls [next $name {*}$args]
        set ns ::[namespace qualifiers [namespace which $cls]]
        foreach cmd {mc mcmax mcexists} {
            oo::define $cls forward $cmd apply [list \
                {cmd args} {tailcall $cmd {*}$args} $ns] \
                ::msgcat::$cmd
            oo::define $cls unexport $cmd
        }
        return $cls
    }
}
</pre>

Now our little package would become:

<pre>
namespace eval ::foo {
    msgcat::mcload $dir/msgs
    msgcat::oobridge create MessageCatalogAware
    oo::class create Foo {
        mixin MessageCatalogAware
        # ...
    }
    oo::class create Bar {
        mixin MessageCatalogAware
        # ...
    }
}
package provide foo 1.0
</pre>

Alternatively, we could plug the capabilities into `oo::define`; the principles of the code are largely the same but the details are little different.

I've been thinking about this more, and I've realised that there is a subtle problem: a superclass and a subclass could be in different namespaces in different packages, and so could reasonably want to use different message catalogs. This means that the bridging code always needs to look up what the context namespace is at runtime, which in turn means that there's no need for clever class manufacturing, and it can all be done with (see section "Implementation" above).

# Credits

* Ren√© Zaumseil: initiative and partial implementation
* Eric Boudallier: alternate implementation
* Donal Fellows: implementation and examples
* Ashok P. Nadkarni: teach me oo by his excellent book


# Copyright

This document has been placed in the public domain.
